#include <MsTimer2.h>

#include <LiquidCrystal.h>
#include <Wire.h> 



/* LCD Consts */

const int numRows = 2;
const int numColumns = 16;
int currVal = 1000;
LiquidCrystal LCD(12, 11, 5,4,3,6);

/* BMP Calibration Consts */

int ac1;
int ac2; 
int ac3; 
unsigned int ac4;
unsigned int ac5;
unsigned int ac6;
int b1; 
int b2;
int mb;
int mc;
int md;

const unsigned  char OSS = 0;

int int_counter = 0;

/* Variables */
long b5; 
short temperature;
long pressure;

// Use these for altitude conversions
const float p0 = 101325;     // Pressure at sea level (Pa)
float altitude;

// For Trending

float pressure_history[5];
float pressure_delta = 0.2;
float press_mov_avg = 0;
float alpha = 0.9;
float temp; 
float exp_press = 1013.0;

boolean to_read = true;
boolean to_display = true;
short display_refresh_rate = 10; // seconds 
short data_refresh_rate = 2;

boolean first_time = true;

// Rressure/Height toggle

const float sea_press = 1013.25;

#define BMPADDR  0x77
#define DISPLAY_REFRESH_RATE 20
#define LONG_TERM_REFRESH_RATE 12

const int modeSwitchPin = 10;
const int counterPin = 9;

const int debounce_interval = 20; // ms
const int long_pressed_thr = 1000; //ms


//////////////////////////////////////////////////////////////////
//                  State Management                            //
//////////////////////////////////////////////////////////////////

// States 

typedef void (*HANDLER_FN_TYPE)();

enum states {
  DISPLAYING_PRESSURE,
  DISPLAYING_HEIGHT,
  SETUP_MODE_BLINK,
  SETUP_MODE_DIGIT,
  RESET_HEIGTH,
  NUM_STATES
};

// Inputs 

enum inputs {
  BUTTON_1_SHORT,
  BUTTON_1_LONG,
  BUTTON_2_SHORT,
  NUM_INPUTS
};

struct state_entry {
  int next_state;
  HANDLER_FN_TYPE action;
};
int current_state = DISPLAYING_PRESSURE; // Initial state

struct state_entry state_entry_table[NUM_STATES][NUM_INPUTS];
HANDLER_FN_TYPE state_display_fn_array[NUM_STATES];

boolean showing_pressure = true;
int decimal_place_index = 0;
int digit_to_blink = -1;

char curr_alt_setting[5] = {'0', '0', '0', '0'};
int curr_alt;

float curr_alt_zero_value;


void change_state(int this_input) {
  HANDLER_FN_TYPE f = state_entry_table[current_state][this_input].action;
  if (f != 0) f();
  if (state_entry_table[current_state][this_input].next_state != -1)
    current_state = state_entry_table[current_state][this_input].next_state;
  //Serial.print(current_state);
}

// Action Handlers 

void toggleDisplayType() {
  showing_pressure = !showing_pressure;
  to_display = true;
}

void handle_setup (void) {
  display_refresh_rate = 1;
  digit_to_blink = 0;
}

void set_disp_to_press () {
  display_refresh_rate = 10;
  display_pressure_state();
  char t [5];
  t[0] = curr_alt_setting[0];
  t[1] = curr_alt_setting[1];
  t[2] = curr_alt_setting[2];
  t[3] = curr_alt_setting[3];
  t[4] = 0;
  
  curr_alt = atoi(t);
  exp_press = get_expected_pressure();
  
  Serial.print(exp_press);
}

void select_digit_to_blink() {
  digit_to_blink ++;
  if (digit_to_blink > 3) 
    digit_to_blink = 0;
}

void increment_digit () {
   increment_char(digit_to_blink);
}

void increment_char(int index) {
  char digit = curr_alt_setting[index];
  digit ++; 
  if (digit > '9') {
    digit = '0';
  }
  curr_alt_setting[index] = digit;
}



// POPULATE State Table

void populateStateTable() {
  
  // Initialize everything to no-op
  
  for (int i = 0; i < NUM_STATES; i++) 
    for (int j = 0; j < NUM_INPUTS; j++) {
      state_entry_table[i][j].next_state = -1;
      state_entry_table[i][j].action = 0;
    }
    
  state_entry_table[DISPLAYING_PRESSURE][BUTTON_1_SHORT].next_state = DISPLAYING_HEIGHT;
  state_entry_table[DISPLAYING_PRESSURE][BUTTON_1_SHORT].action = toggleDisplayType;
  
  state_entry_table[DISPLAYING_HEIGHT][BUTTON_1_SHORT].next_state = DISPLAYING_PRESSURE;
  state_entry_table[DISPLAYING_HEIGHT][BUTTON_1_SHORT].action = toggleDisplayType;
  
  state_entry_table[DISPLAYING_PRESSURE][BUTTON_1_LONG].next_state = SETUP_MODE_BLINK;
  state_entry_table[DISPLAYING_PRESSURE][BUTTON_1_LONG].action = handle_setup;
  
  state_entry_table[DISPLAYING_HEIGHT][BUTTON_1_LONG].next_state = SETUP_MODE_BLINK;
  state_entry_table[DISPLAYING_HEIGHT][BUTTON_1_LONG].action = handle_setup;
  
  state_entry_table[SETUP_MODE_BLINK][BUTTON_1_LONG].next_state = DISPLAYING_PRESSURE;
  state_entry_table[SETUP_MODE_BLINK][BUTTON_1_LONG].action = set_disp_to_press;
 
  state_entry_table[SETUP_MODE_BLINK][BUTTON_1_SHORT].next_state = SETUP_MODE_DIGIT;
  state_entry_table[SETUP_MODE_BLINK][BUTTON_1_SHORT].action = select_digit_to_blink;
 
  state_entry_table[SETUP_MODE_DIGIT][BUTTON_1_SHORT].next_state = SETUP_MODE_DIGIT;
  state_entry_table[SETUP_MODE_DIGIT][BUTTON_1_SHORT].action = select_digit_to_blink;
 
  state_entry_table[SETUP_MODE_DIGIT][BUTTON_1_LONG].next_state = DISPLAYING_PRESSURE;
  state_entry_table[SETUP_MODE_DIGIT][BUTTON_1_LONG].action = set_disp_to_press;
  
  state_entry_table[SETUP_MODE_DIGIT][BUTTON_2_SHORT].next_state = SETUP_MODE_DIGIT;
  state_entry_table[SETUP_MODE_DIGIT][BUTTON_2_SHORT].action = increment_digit;
  
  // Task Array
  state_display_fn_array[DISPLAYING_PRESSURE] = display_pressure_state;
  state_display_fn_array[DISPLAYING_HEIGHT] = display_altitude_state;
  state_display_fn_array[SETUP_MODE_BLINK] = display_all_blink_digits;
  state_display_fn_array[SETUP_MODE_DIGIT] = blink_individual_digit;
}



////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////
//           State Display Funcitons                             //
///////////////////////////////////////////////////////////////////

void display_pressure_state(void) {
  LCD.clear();
  printPressure(press_mov_avg);
  printTemparature(temp);
}

void display_altitude_state(void) {
  LCD.clear();
  printAltitude(getAltitude(press_mov_avg, temp));
  printTemparature(temp);
}

void display_all_blink_digits() {
  //Serial.print("HOHOHO");
  static boolean show = true;
  LCD.clear();
 
  LCD.print("SET Altitude");
  if (show == true) {
    LCD.setCursor(0,1);
    LCD.print(" ");
    LCD.print(curr_alt_setting);
  }
  show = !show;
}

void blink_individual_digit() {
  LCD.clear();
  LCD.print("SET Altitude");
  static boolean show = true;
  
  LCD.setCursor(1, 1);
  LCD.print(curr_alt_setting);
  if (!show) {
   LCD.setCursor(1 + digit_to_blink, 1);
   LCD.print(" ");
  }
  show = !show;
}

///////////////////////////////////////////////////////////////////

void setup () {
  
  populateStateTable();

  pinMode(modeSwitchPin, INPUT);
  digitalWrite(modeSwitchPin, HIGH);
  pinMode(counterPin, INPUT);
  digitalWrite(counterPin, HIGH);
  
  //attachInterrupt(0, togglePressDisplayType, FALLING);
  Serial.begin(9600);
  LCD.begin(numColumns, numRows);
  Wire.begin();
  readBMPCalibValues();

  MsTimer2::set(500, oneSecond);
  MsTimer2::start();
}



void loop() {

  int  mode_key_press_time = modeKeyPressed();
  
  if (mode_key_press_time > long_pressed_thr) {
    //Serial.print("HERE");
    change_state(BUTTON_1_LONG);
  }
  else if (mode_key_press_time > debounce_interval) {
    change_state(BUTTON_1_SHORT);
  }   
  
  int  counter_kp_time = counterKeyPressed();
  
  if (counter_kp_time > long_pressed_thr) {
    //change_state(BUTTON_1_LONG);
  }
  else if (counter_kp_time > debounce_interval) {
    change_state(BUTTON_2_SHORT);
  }   
  
  
  if (to_read || first_time ){
    temp = (float)getTemp(bmpReadUncompTemp())/10;
    float pressure = (float)getPressure(bmpReadUncompPr())/100;

    to_read = false;
    first_time = false;

    if (press_mov_avg == 0) {
      press_mov_avg = pressure;
    } 
    else {
      press_mov_avg = alpha * pressure + (1-alpha) * press_mov_avg;
    }
//    Serial.print(temp);
//    Serial.print(",");
//    Serial.println(pressure);
  }
  
  if (first_time || to_display) {
      HANDLER_FN_TYPE f = state_display_fn_array[current_state];
      f();
      to_display = false;
    } 
}


//////////////////////////////////////////////////////////////////////////
//              Key Handlers                                            //
//////////////////////////////////////////////////////////////////////////


int modeKeyPressed() {
  static int last_state = 1;
  static long last_timestamp;
  
  if (digitalRead(modeSwitchPin) != last_state) {
    last_state = (1 - last_state);

    if (last_state == 1) {
      return (millis() - last_timestamp);
    }
    else {
      last_timestamp = millis();
      return 0;
    }
  }
} 
  
  int counterKeyPressed() {
  static int last_state = 1;
  static long last_timestamp;

  if (digitalRead(counterPin) != last_state) {
    last_state = (1 - last_state);

    if (last_state == 1) {
      return (millis() - last_timestamp);
    }
    else {
      last_timestamp = millis();
      return 0;
    }
  } 
} 

//////////////////////////////////////////////////////////////////////////
//                      Timer Handlers                                   //
//////////////////////////////////////////////////////////////////////////

boolean output = HIGH;

void oneSecond(void) {

  static short display_tick = 0;
  static short data_tick = 0;
  
  display_tick ++; 
  if (display_tick >= display_refresh_rate) {
    to_display = true;
    display_tick = 0;
  }
  
  data_tick ++; 
  if (data_tick >= data_refresh_rate) {
    to_read = true;
    data_tick = 0;
  }
}

//////////////////////////////////////////////////////////////////////////
//                Display Functions                                     //
//////////////////////////////////////////////////////////////////////////

void clearLCD(void) {
  LCD.clear();
}

void printPressure(float pressure) {
  LCD.setCursor(0,0);
  LCD.print("P:");
  LCD.print(pressure, 1);
  LCD.print("hp");
  
  if ((pressure - exp_press) < - 3) 
    LCD.print("<");
  else if ( ((pressure - exp_press) > -3 ) && ((pressure - exp_press) < 3)) 
    LCD.print("=");
  else if ((pressure - exp_press > 3))
    LCD.print(">");
}

void printAltitude(float altitude) {
  LCD.setCursor(0,0);
  LCD.print("H:");
  LCD.print(altitude, 1);
  LCD.print("m ");
}

void printTemparature(float temp){
  LCD.setCursor(0,1);
  LCD.print("T:");
  LCD.print(temp,  1);
  LCD.print("c");
}

void printHumidity(float humid) {
  LCD.setCursor(8,1);
  LCD.print("R:" );
  LCD.print(humid,1);
  LCD.print("%");
}

//////////////////////////////////////////////////////////////////////////
//                    Sensor Functions                                  //
//////////////////////////////////////////////////////////////////////////

long getPressure(unsigned long up)
{
  long x1, x2, x3, b3, b6, p;
  unsigned long b4, b7;

  b6 = b5 - 4000;
  // Calculate B3
  x1 = (b2 * (b6 * b6)>>12)>>11;
  x2 = (ac2 * b6)>>11;
  x3 = x1 + x2;
  b3 = (((((long)ac1)*4 + x3)<<OSS) + 2)>>2;

  // Calculate B4
  x1 = (ac3 * b6)>>13;
  x2 = (b1 * ((b6 * b6)>>12))>>16;
  x3 = ((x1 + x2) + 2)>>2;
  b4 = (ac4 * (unsigned long)(x3 + 32768))>>15;

  b7 = ((unsigned long)(up - b3) * (50000>>OSS));
  if (b7 < 0x80000000)
    p = (b7<<1)/b4;
  else
    p = (b7/b4)<<1;

  x1 = (p>>8) * (p>>8);
  x1 = (x1 * 3038)>>16;
  x2 = (-7357 * p)>>16;
  p += (x1 + x2 + 3791)>>4;

  return p;
}

short getTemp(unsigned int ut)
{

  unsigned int ut1 = 123;

  long x1, x2;

  x1 = (((long)ut - (long)ac6)*(long)ac5) >> 15;
  x2 = ((long)mc << 11)/(x1 + md);
  b5 = x1 + x2;

  return ((b5 + 8)>>4);  
}


unsigned long bmpReadUncompPr()
{
  unsigned char msb, lsb, xlsb;
  unsigned long up = 0;

  // Write 0x34+(OSS<<6) into register 0xF4
  // Request a pressure reading w/ oversampling setting
  Wire.beginTransmission(BMPADDR);
  Wire.write(0xF4);
  Wire.write(0x34 + (OSS<<6));
  Wire.endTransmission();

  // Wait for conversion, delay time dependent on OSS
  delay(2 + (3<<OSS));

  // Read register 0xF6 (MSB), 0xF7 (LSB), and 0xF8 (XLSB)
  Wire.beginTransmission(BMPADDR);
  Wire.write(0xF6);
  Wire.endTransmission();
  Wire.requestFrom(BMPADDR, 3);

  // Wait for data to become available
  while(Wire.available() < 3)
    ;
  msb = Wire.read();
  lsb = Wire.read();
  xlsb = Wire.read();

  up = (((unsigned long) msb << 16) | ((unsigned long) lsb << 8) | (unsigned long) xlsb) >> (8-OSS);

  return up;
}

unsigned int bmpReadUncompTemp()
{
  unsigned int ut;
  // Write 0x2E into Register 0xF4
  // This requests a temperature reading
  Wire.beginTransmission(BMPADDR);
  Wire.write(0xF4);
  Wire.write(0x2E);
  Wire.endTransmission();
  // Wait at least 4.5ms
  delay(5);

  // Read two bytes from registers 0xF6 and 0xF7
  ut = bmpReadInt(0xF6);
  return ut;
}

char bmpReadChar (unsigned char address) {

  Wire.beginTransmission(BMPADDR);

  Wire.write(address);
  Wire.endTransmission();

  Wire.requestFrom(BMPADDR,1);
  while(!Wire.available());
  return Wire.read();
}

int bmpReadInt(unsigned char address) {

  unsigned char msb, lsb;

  Wire.beginTransmission(BMPADDR);
  Wire.write(address);
  Wire.endTransmission();

  Wire.requestFrom(BMPADDR,2);
  while(Wire.available() < 2);

  msb = Wire.read();
  lsb = Wire.read();

  return(int)((msb << 8 ) | lsb); 
}

void readBMPCalibValues () {

  ac1 = bmpReadInt(0xAA);
  ac2 = bmpReadInt(0xAC);
  ac3 = bmpReadInt(0xAE);
  ac4 = bmpReadInt(0xB0);
  ac5 = bmpReadInt(0xB2);
  ac6 = bmpReadInt(0xB4);
  b1 = bmpReadInt(0xB6);
  b2 = bmpReadInt(0xB8);
  mb = bmpReadInt(0xBA);
  mc = bmpReadInt(0xBC);
  md = bmpReadInt(0xBE);
}

float getRH() {
  return 99;
}

float getAltitude(float press, float temp) {
  return ((pow((sea_press / press), 1/5.257) - 1.0) * (temp + 273.15)) / 0.0065;
}

//////////////////////////////////////////////////////////////////////////////
//                      End Sensor Functions                                //
//////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////
//                      Utility Functions                                   //
//////////////////////////////////////////////////////////////////////////////

float get_expected_pressure() {
  float p = sea_press * pow((1 - (float)curr_alt/44330), 5.25);
  return p;
}

//////////////////////////////////////////////////////////////////////////////




